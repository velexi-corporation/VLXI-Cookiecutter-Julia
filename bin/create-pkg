#!/usr/bin/env bash
# -*- mode: julia -*-
#=
exec julia --startup-file=no -q --compile=min -O0 \
           --project=`dirname "${BASH_SOURCE[0]}"` "${BASH_SOURCE[0]}" "$@"
=#

# ------------------------------------------------------------------------------------------
#   Copyright (c) 2020-2022 Velexi Corporation
#
#   Licensed under the Apache License, Version 2.0 (the "License");
#   you may not use this file except in compliance with the License.
#   You may obtain a copy of the License at
#
#       http://www.apache.org/licenses/LICENSE-2.0
#
#   Unless required by applicable law or agreed to in writing, software
#   distributed under the License is distributed on an "AS IS" BASIS,
#   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
#   See the License for the specific language governing permissions and
#   limitations under the License.
# ------------------------------------------------------------------------------------------

# --- Imports

# Standard library
using Pkg

# External packages
using ArgParse
using PkgTemplates

# --- Main program

function main()
    # --- Preparations

    # Set up environment for this script
    Pkg.instantiate()

    # --- Define CLI

    # Define command-line arguments
    description = "Create new Julia package"
    arg_table = ArgParseSettings(; description=description)
    @add_arg_table! arg_table begin
        "--julia-version", "-j"
        help = "minimum Julia version"
        arg_type = VersionNumber
        default = v"1.6"

        "--dest-dir", "-d"
        help = "directory where Julia package will be created"
        default = "."

        "--license", "-l"
        help = "package license"
        default = "ASL"

        "--ci-include-x86", "--x86"
        help = "include x86 architecture in CI tests (default: false)"
        action = :store_true

        "--ci-disable-codecov", "-C"
        help = "disable upload of coverage results to Codecov (default: false)"
        action = :store_true

        "pkg_name"
        help = "package name"
        required = true
    end

    # Parse command-line arguments
    args::Dict = parse_args(ARGS, arg_table)
    pkg_name::String = args["pkg_name"]

    julia_version::VersionNumber = args["julia-version"]
    dst_dir::String = args["dest-dir"]
    license::String = args["license"]
    ci_include_x86::Bool = args["ci-include-x86"]
    ci_enable_codecov::Bool = !args["ci-disable-codecov"]

    # --- Create package

    # Define package template
    plugins = [
        License(; name=license),
        Git(; ssh=true),
        Documenter{GitHubActions}(),
        GitHubActions(; x86=ci_include_x86),
    ]
    if ci_enable_codecov
        push!(plugins, Codecov())
    end

    pkg_template = Template(; julia=julia_version, dir=dst_dir, plugins=plugins)

    # Define package template
    pkg_template(pkg_name)

    # Upgrade manifest
    Pkg.activate(joinpath(dst_dir, pkg_name))
    Pkg.upgrade_manifest()

    return nothing
end

# --- Run main program

if abspath(PROGRAM_FILE) == @__FILE__
    main()
end
